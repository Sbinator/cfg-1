#!/usr/bin/env hy
;; -*- mode:hy -*-

(import [pylast [LastFMNetwork]])
(import json)
(import yaml)
(import [mpm.mpm [Mpm]])
(import [mpm.db :as db])
(import [high.utils [*]])
(import [time [time]])
(require [high.macros [*]])

(def mpm-config #p"~/.mpm.d/config")
(def log-file #p"~/.mpm.d/play-log")
(def scrobbled-at-file #p"~/.mpm.d/scrobbled-at")
(def last-fm-file #p"~/.mpm.d/last-fm.json")
(def mpm (Mpm (with-fp mpm-config (yaml.load fp))))

(defn read-log [file-name]
  (emap (fn [line] (emap int (.split line ",")))
        (with-fp file-name
          (efilter (fn [line] (> (len line) 0)) (.split (fp.read) "\n")))))

(defn item-present? [log-entry]
  "Check if the item in entry is present in db"
  (db.song-present? mpm.database :id (second log-entry)))

(defn get-item [log-entry]
  "Get the item represented in the entry"
  (let [song (db.get-song mpm.database (second log-entry))]
       {"artist" (get song "artist")
        "title" (get song "title")
        "album" (get song "album")
        "timestamp" (first log-entry)}))

(defn filter-entries [log-entries timestamp]
  "Remove entries older than the timestamp"
  (efilter (fn [entry] (> (get (get-item entry) "timestamp") timestamp))
           (efilter item-present? log-entries)))

(defn scrobble [log-entries lastfm-network]
  (let [items (emap get-item log-entries)]
       (if (= (len items) 0)
           (print "Nothing to do")
           (do (print (+ "Scrobbling " (str (len items)) " items"))
               (lastfm-network.scrobble-many items)))))

(defn update-last-scrobble [file-name]
  (with [fp (open file-name "w")]
    (fp.write (str (int (time))))))

(defmain [&rest args]
  (let [lastfm-config (with-fp last-fm-file (json.load fp))
        log-entries (read-log log-file)
        last-scrobble (with-fp (ensure-file scrobbled-at-file "0") (int (fp.read)))
        lastfm-network (apply LastFMNetwork [] lastfm-config)]
       (scrobble (filter-entries log-entries last-scrobble) lastfm-network)
       (update-last-scrobble scrobbled-at-file)
       (exit 0)))
